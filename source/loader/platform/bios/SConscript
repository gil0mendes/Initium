#
# Copyright (C) 2014 Gil Mendes
#
# Permission is hereby granted, free of charge, to any person obtaining a copy of
# this software and associated documentation files (the "Software"), to deal in
# the Software without restriction, including without limitation the rights to
# use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
# the Software, and to permit persons to whom the Software is furnished to do so,
# subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
# FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
# COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
# IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
# CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
#

Import('config', 'defaults', 'env')
from util import FeatureSources

sources = FeatureSources(config, [
    'console.c',
    'platform.c',
    'start.S',
])

# Build the stage 1 loaders.
env.LDScript('stage1/stage1.ld', 'stage1/stage1.ld.in')
stage1s = ['cdboot']
for f in stage1s:
    name = 'stage1/%s' % (f)

    env.Program('%s.elf' % (name), '%s.S' % (name), LIBS = [],
        LINKFLAGS = env['LINKFLAGS'] + ['-Wl,-T,${TARGET.dir}/stage1.ld', '-Wl,-N'])
    Depends('%s.elf' % (name), File('stage1/stage1.ld'))

    target = env.Command([name, '%s.lst' % (name)], '%s.elf' % (name), [
        Action("$OBJCOPY -O binary --strip-all $SOURCE ${TARGETS[0]}", '$GENCOMSTR'),
        Action('$OBJDUMP -d -m i386 -M i8086 $SOURCE > ${TARGETS[1]}', None),
    ])

    defaults.append(target)

# Set the linker script path.
env['LDSCRIPT'] = env.LDScript('link.ld', 'link.ld.in')

Return('sources')
