#
# The MIT License (MIT)
#
# Copyright (c) 2014-2015 Gil Mendes
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#

vars = ['env', 'config', 'defaults']

Import(*vars)
from util import FeatureSources

sources = FeatureSources(config, [
    ('TARGET_HAS_DISK', 'fs/ext2.c'),
    ('TARGET_HAS_DISK', 'fs/fat.c'),
    ('TARGET_HAS_DISK', 'fs/iso9660.c'),

    'lib/allocator.c',
    'lib/printf.c',
    'lib/qsort.c',
    'lib/string.c',
    'lib/charset.c',
    'lib/line_editor.c',

    ('TARGET_HAS_INITIUM64', 'TARGET_HAS_INITIUM32', 'loader/initium.c'),

    ('TARGET_HAS_DISK', 'partition/gpt.c'),
    ('TARGET_HAS_DISK', 'partition/mbr.c'),

    'config.c',
    'console.c',
    'device.c',
    ('TARGET_HAS_DISK', 'disk.c'),
    'error.c',
    'fs.c',
    'main.c',
    'memory.c',
    ('TARGET_HAS_UI', 'menu.c'),
    'shell.c',
    'time.c',
    ('TARGET_HAS_UI', 'ui.c'),
    ('TARGET_HAS_VIDEO', 'video.c'),
])

# Set the include search paths.
env['CPPPATH'] = [
    Dir('include'),
    Dir('arch/%s/include' % (config['BOOTARCH'])),
    Dir('platform/%s/include' % (config['PLATFORM'])),
]

# Include the generated configuration header.
env['CCFLAGS'] += ['-include', 'build/%s-%s/config.h' % (config['ARCH'], config['PLATFORM'])]
env['ASFLAGS'] += ['-include', 'build/%s-%s/config.h' % (config['ARCH'], config['PLATFORM'])]

# Platform code needs the location of the loader binary.
env['LOADER'] = File('loader.elf')

# Get the list of source files from the architecture/platform.
arch_sources = SConscript(dirs = ['arch/' + config['BOOTARCH']], exports = vars)
platform_sources = SConscript(dirs = ['platform/' + config['PLATFORM']], exports = vars)
drivers_sources = SConscript(dirs = ['drivers'], exports = vars)
sources = arch_sources + platform_sources + drivers_sources + sources
objects = map(env.Object, sources)

# Link the loader.
linkflags = env['LINKFLAGS'] + ['-Wl,-T,$LDSCRIPT']
target = env.Program('loader.elf', objects, LINKFLAGS = linkflags)
Depends(target, env['LDSCRIPT'])

# Generate various information files.
env.AddPostAction(target, [
    Action('$OBJDUMP -d $TARGET > ${TARGET.base}.lst', None),
    Action('$READELF -aW $TARGET > ${TARGET.base}.rde', None),
    Action('$NM -C $TARGET | sort > ${TARGET.base}.sym', None),
])
